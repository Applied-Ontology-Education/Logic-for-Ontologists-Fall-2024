# This checks whether all the realizable entities are connected to a process through "has realization"
# Level 6

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT ?subclass (CONCAT("Error: Realizable entity ", STR(?label), " doesn't have a process realizing it.") AS ?errorMessage)
WHERE {
    ?subclass rdfs:subClassOf+ obo:BFO_0000017 .
    ?subclass rdfs:label ?label.
    FILTER (!ISBLANK(?subclass)) .

    FILTER NOT EXISTS {
        ?subclass rdfs:subClassOf [
            rdf:type owl:Restriction ;
            owl:onProperty obo:BFO_0000054 ;
            owl:someValuesFrom ?targetClass 
        ].
    }
}
